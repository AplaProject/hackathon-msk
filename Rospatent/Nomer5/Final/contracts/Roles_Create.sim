contract Roles_Create {
    data {
        role_name string
        role_type int
        company_id int "optional"
    }
    
    conditions {
        // system role
        if ($role_type == 3) {
            warning "System role can not be created"
        }
        
        // company role
        if ($role_type == 4) {
            
            // check param
            if ($company_id == 0) {
                warning "On this page you can not create a role for the company. Go to the company's view page to create a role"
            }
            
            // check creator of the company
            if ($company_id != $key_id) {
                warning "Sorry, you are not the creator of this company"
            }
        }
        
        // check member
        $member_map = DBFind("members").Where("id = $", $key_id).Row()
        if ($member_map["id"] == nil) {
            warning "You are not a member of the ecosystem"
        }
    }
    
    action {
        if ($role_type == 4) {
            $result = DBInsert("roles_list", "role_name,role_type,creator_id,creator_name,creator_avatar,company_id,timestamp date_create,delete",
            $role_name, $role_type, $key_id, $member_map["member_name"], $member_map["avatar"], $company_id, $block_time, 0)
        } else {
            $result = DBInsert("roles_list", "role_name,role_type,creator_id,creator_name,creator_avatar,company_id,timestamp date_create,delete",
            $role_name, $role_type, $key_id, $member_map["member_name"], $member_map["avatar"], 0, $block_time, 0)
        }
    }
}