DBFind(parameters).Where(name='founder_account').Vars(founder)
SetTitle($demo_title$)
Div(content-wrapper){
    Div(row){
        Div(col-lg-8 col-lg-offset-2){
            Div(h4){LangRes(input)}
            Div(panel panel-primary){
                Form(panel-body){
                    Div(row){
                        Div(col-sm-6){
                            Input(Name: SomeText, Type:text, Placeholder:Enter text, Value: Hello World!)
                        }
                        Div(col-sm-6){
                            Div(input-group){
                                DBFind(Name: members, Source: src_member)
                                Select(Name: MemberId, Source: src_member, NameColumn: member_name, ValueColumn: id)
                                Div(input-group-btn){
                                    Button(Body: $send_notify$, Contract: DemoNotificationSingle, Class:btn btn-primary, Params: "MemberId=Val(MemberId)")
                                }
                            }
                        }
                    }
                }
            }
            
            Div(h4){LangRes(code)}
            Div(panel panel-primary){
                Div(panel-body){
                    If(#founder_value#==#key_id#){
                        Div(h5){
                            DBFind(blocks).Where(name='demo_block_sendMessage').Vars(page)
                            LinkPage(Page: "edit-block", PageParams: "id=#page_id#"){ $edit_page$ }
                        }
                    }.Else{
                        Div(h5 text-bold){$page_code$}
                    }
                    Div(){
Code(
    Form(panel-body){
        Div(col-sm-6){
            Input(Name: SomeText, Type:text, Placeholder:Enter text, Value: Hello World!)
        }
        Div(col-sm-6){
            Div(input-group){
                DBFind(Name: members, Source: src_member)
                Select(Name: MemberId, Source: src_member, NameColumn: member_name, ValueColumn: id)
                Div(input-group-btn){
                    Button(Body: Send Notification, Contract: DemoNotificationSingle, Class:btn btn-primary, Params: "MemberId=Val(MemberId)")
                }
            }
        }
    }
)
                    }
                    If(#founder_value#==#key_id#){
                        Div(h5){
                            LinkPage(Page: "edit-contract", PageParams: "name=DemoNotificationSingle"){ $edit_contract$ }
                        }
                    }.Else{
                        Div(h5 text-bold){$contract_code$}
                        
                    }
Code(
    contract DemoNotificationSingle {
        data {
            MemberId int
            SomeText string
        }
        
        conditions {
            $iconName = "fa-bell"
            $textHeader="You got a message"
            $pageName="demo_page_message"
            $paramsVal= Sprintf("SomeText=%v", $SomeText)
            $textBody = $SomeText
        }
        
        action {
            Notifications_Single_Send("MemberId,IconName,TextHeader,TextBody,PageName,ParamsVal", $MemberId, $iconName, $textHeader, $textBody, $pageName, $paramsVal)
        }
    }
)
                }
            }
            
            Div(h4){LangRes(description)}
            Div(panel panel-primary){
                Div(panel-body){
                    LangRes(demo_description_sendMessage)
                }
            }
        }
    }
}